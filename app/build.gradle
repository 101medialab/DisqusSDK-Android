apply plugin: 'com.android.application'

android {
    def configConstants = new Properties()
    def Props = file("auth.properties")
    if (Props.exists()) {
        Props.withInputStream {
            stream -> configConstants.load(stream)
        }
    }

    compileSdkVersion project.ANDROID_BUILD_SDK_VERSION as int
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.hkm.dis"
        minSdkVersion project.ANDROID_BUILD_MIN_SDK_VERSION
        targetSdkVersion project.ANDROID_BUILD_TARGET_SDK_VERSION
        versionCode 4
        versionName "1.3"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        defaultConfig {
            applicationId "com.dis.demo"
            versionName defaultConfig.versionName
            buildConfigField "String", "DISQUS_API_KEY", "\"${configConstants.DISQUS_API_KEY}\""
            buildConfigField "String", "DISQUS_SECRET", "\"${configConstants.DISQUS_SECRET}\""
            buildConfigField "String", "DISQUS_DEFAULT_ACCESS", "\"${configConstants.DISQUS_DEFAULT_ACCESS}\""
            buildConfigField "String", "DISQUS_REDIRECT_URI", "\"${configConstants.DISQUS_REDIRECT_URI}\""
            buildConfigField "String", "DISQUS_REDIRECT_CALLBACK", "\"${configConstants.DISQUS_REDIRECT_CALLBACK}\""
        }
    }
}
repositories {
    maven { url 'https://dl.bintray.com/jjhesk/maven' }
    flatDir {
        dirs '../libs'
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile supportDependencies.annotation
    compile supportDependencies.appCompat
    compile supportDependencies.support

    compile 'org.apache.commons:commons-lang3:3.1'

    compile 'com.squareup.okhttp:okhttp-urlconnection:2.3.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'me.drakeet.library:crashwoodpecker:2.1.1'

    fileTree(dir: '../libs', includes: ['*.aar']).each {
        File file ->
            dependencies.add('compile', [name: file.name.lastIndexOf('.').with { it != -1 ? file.name[0..<it] : file.name }, ext: 'aar'])
    }

    compile project(':disqus')
}
